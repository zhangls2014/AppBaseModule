plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

// 读取 git 的提交数量作为版本号
static def gitVersionCode() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    (cmd.execute().text.trim() ?: "1").toInteger()
}
// 读取 git 的 commit tag 作为应用的版本名，如果后面
static def gitVersionTag() {
    def cmd = 'git describe --tags'
    def tag = cmd.execute().text.trim().split("-")
    if (tag.length > 1) {
        tag.first() + "-develop"
    } else {
        tag.first()
    }
}

android {
    compileSdk Versions.compileSdk
    buildToolsVersion Versions.buildTool

    defaultConfig {
        minSdk Versions.minSdk
        targetSdk Versions.targetSdk
        versionCode gitVersionCode()
        versionName gitVersionTag()

        consumerProguardFiles "consumer-rules.pro"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    sourceSets {
        main {
            java.srcDirs = ['src/main/kotlin']
        }
    }
    lintOptions {
        disable 'SpUsage', 'RtlHardcoded'
    }
}

dependencies {
    // 测试、Debug
    testImplementation Deps.junit
    androidTestImplementation Deps.extJunit
    androidTestImplementation Deps.espresso

    api Deps.coroutinesAndroid
    api Deps.coreKtx
    api Deps.materialDesign
    api Deps.constraintLayout
    api Deps.navigationFragmentKtx

    // 网络请求框架：Retrofit + Gson
    api Deps.gson
    api Deps.retrofit2
    api Deps.retrofit2ConverterGson
    api Deps.okHttpLogging
    api Deps.utilcodex
    // 日志框架
    api Deps.glog
    api Deps.timber
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                groupId = 'com.zhangls'
                artifactId = 'base'
                version = gitVersionTag()
            }
        }
    }
}
